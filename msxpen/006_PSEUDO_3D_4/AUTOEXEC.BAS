1 ' Copyright (c) 2023 emef220 
2 ' Released under the MIT license
3 ' https://opensource.org/licenses/mit-license.php
4 '
5 ' -----------------------------------------------------------
6 ' PSEUDO 3D Palette Animation ( MSX2 or later)
7 ' -----------------------------------------------------------
8 '
10 COLOR 15,0,1:DEFINT A-T
11 SCREEN 5
12 A=2:C(0)=2:C(1)=6
110 ' DRAW LEFT SCREEN
111 Y=0:K=1:L=0:N=0
112 FOR T= 0 TO 96
120 ' INIT M (Y-AXIS COUNTER) 
125 M=0
130 ' INCREASE K (SCALE Y-AXIS)
134 IF Y>4 THEN K=2:
135 IF Y>16 THEN K=3:
136 IF Y>50 THEN K=4:
140 ' SWAP PALETTE
141 IF L=0 THEN GOSUB 610
160 ' REPEAT LINE K TIMES
161 FOR V=0 TO K-1
162 ' EXIT THE LOOP WHEN Y > 96
163 IF Y>96 THEN Y=0:GOTO 211 'GO TO RIGHT SCREEN
165 M=M+1:Y=T+M:B=0':PRINT Y",V" V",K" K ",L" L",M"M"N"N
170 ' DRAW X LINE (0-127)
171 FOR X=127 TO 0 STEP -1 
173 S=127-X:'PRINT",X" X",Y" Y
174 IF S=0 THEN D=0:PSET(X,Y+111),C(3)
175 IF S=B THEN GOSUB 810
176 IF S=127 THEN GOSUB 810
177 NEXT
178 NEXT V
180 'UPDATE L,N,T
183 IF K=1 THEN L=NMOD2
184 IF K>1 THEN L=NMODK
185 N=N+1: T=T+M-1
187 NEXT T
210 ' DRAW RIGHT SCREEN
211 Y=0:K=1:L=0:N=0
212 FOR T= 0 TO 96
220 ' INIT M (Y-AXIS COUNTER) 
225 M=0
230 ' INCREASE K (SCALE Y-AXIS)
234 IF Y>4 THEN K=2:
235 IF Y>16 THEN K=3:
236 IF Y>50 THEN K=4:
240 ' SWAP PALETTE
241 IF L=0 THEN GOSUB 610
260 ' REPEAT LINE K TIMES
261 FOR V=0 TO K-1
262 ' EXIT THE LOOP WHEN Y > 96
263 IF Y>96 THEN Y=0:GOTO 399 'GO TO PALETTE ANIMATION PART
265 M=M+1:Y=T+M:B=0:'PRINT Y",V" V",K" K ",L" L",M"M"N"N
270 ' DRAW X LINE (128-255)
271 FOR X=0 TO 127 STEP 1
273 S=127-X:'PRINT",X" X",Y" Y
274 IF X=0 THEN D=1:PSET(X+128,Y+111),C(3)
275 IF X=B THEN GOSUB 910
276 IF X=127 THEN GOSUB 910
277 NEXT
278 NEXT V
280 'UPDATE L,N,T
283 IF K=1 THEN L=NMOD2
284 IF K>1 THEN L=NMODK
285 N=N+1: T=T+M-1
287 NEXT T
399 GOTO 1531
600 'SUBROUTINE FOR SWAPPING PALETTE
610 C(2)=C(0):C(0)=C(1):C(1)=C(2):RETURN
800 'SUBROUTINE FOR DRAWING LEFT SCREEN
810 GOSUB 1001:LINE-(X,Y+111),C(3):
811 F=(0.28*Y)+5*(SIN(3.1415927*X/512)):B=S+A+F:RETURN
900 'SUBROUTINE FOR DRAWING RIGHT SCREEN
910 GOSUB 1001:LINE-(X+128,Y+111),C(3):
911 F=(0.28*Y)+5*(SIN(3.1415927*S/512)):B=X+A+F:RETURN
1000 'SUBROUTINE FOR CALCULATING INDEX OF PALETTE
1001 D=D+1:C(3)=C(DAND1)+L: RETURN
1500 ' PALETTE ANIMATION
1530 ' CONFIGURE COLORS
1531 COLOR=(10,6,6,3):COLOR=(11,6,6,4):COLOR=(12,4,6,3):COLOR=(13,6,6,4):COLOR=(14,4,6,4)
1910 ' BG
1911 FOR Y=0 TO 111
1912 LINE(0,Y)-(255,Y),13
2000 NEXT Y
2015 BSAVE "PSD3D4.SC5",0,256*212-1,S
2030 ' INIT PALETTE COLOR
2032 'A=7:B=7:C=7:D=7:E=0:F=0:G=0:H=0:J=0:K=0:L=0:M=0:N=0:O=0:P=0:S=&H26:T=&H2:U=&H46:V=&H6
2036 P(0)=&H26:P(1)=&H26:P(2)=&H26:P(3)=&H26:P(4)=&H46:P(5)=&H46:P(6)=&H46:P(7)=&H46
2037 Q(0)=&H2:Q(1)=&H2:Q(2)=&H2:Q(3)=&H2:Q(4)=&H6:Q(5)=&H6:Q(6)=&H6:Q(7)=&H6
2040 'FRAME 1
2041 GOSUB 2123
2042 GOSUB 2216
2050 'FRAME 2
2051 GOSUB 2124
2052 GOSUB 2216
2060 'FRAME3
2061 GOSUB 2125
2062 GOSUB 2216
2070 'FRAME 4
2071 GOSUB 2126
2072 GOSUB 2216
2080 'FRAME 5
2081 GOSUB 2123
2085 GOSUB 2216
2090 'FRAME 6
2091 GOSUB 2124
2095 GOSUB 2216
2100'FRAME 7
2101 GOSUB 2125
2102 GOSUB 2216
2110'FRAME 8
2111 GOSUB 2126
2112 GOSUB 2216
2117 GOTO 2041
2122 'SHIFT PALETTES
2123 T=P(0):P(0)=P(4):P(4)=T:T=Q(0):Q(0)=Q(4):Q(4)=T:RETURN
2124 T=P(1):P(1)=P(5):P(5)=T:T=Q(1):Q(1)=Q(5):Q(5)=T:RETURN
2125 T=P(2):P(2)=P(6):P(6)=T:T=Q(2):Q(2)=Q(6):Q(6)=T:RETURN
2126 T=P(3):P(3)=P(7):P(7)=T:T=Q(3):Q(3)=Q(7):Q(7)=T:RETURN
2210' ASSIGN PALETTES 
2211 'COLOR=(2,A,A,A):COLOR=(3,B,B,B):COLOR=(4,C,C,C):COLOR=(5,D,D,D)
2212 'COLOR=(6,E,E,E):COLOR=(7,F,F,F):COLOR=(8,G,G,G):COLOR=(9,H,H,H)
2216 VDP(17)=2:OUT&H9A,P(0):OUT&H9A,Q(0):VDP(17)=3:OUT&H9A,P(1):OUT&H9A,Q(1):VDP(17)=4 :OUT&H9A,P(2):OUT&H9A,Q(2):VDP(17)=5:OUT&H9A,P(3):OUT&H9A,Q(3)
2217 VDP(17)=6:OUT&H9A,P(4):OUT&H9A,Q(4):VDP(17)=7:OUT&H9A,P(5):OUT&H9A,Q(5):VDP(17)=8:OUT&H9A,P(6):OUT&H9A,Q(6):VDP(17)=9:OUT&H9A,P(7):OUT&H9A,Q(7)
2220 RETURN
3010 I$=INPUT$(1)