1 ' Copyright (c) 2023 emef220 
2 ' Released under the MIT license
3 ' https://opensource.org/licenses/mit-license.php
4 '
5 ' --------------------------------------------------------------------------
6 ' Hyperspace by Palette Animation ( MSX2 or later) - Fixed Width Version -
7 ' --------------------------------------------------------------------------
8 '
10 COLOR 15,0,1:DEFINT A-Z:K=4:L=0
20 SCREEN 5
100 'MAIN ROUTINE FOR DRAWING SCREEN
111 FOR S1=0 TO 255 STEP 8
112 R1=INT(RND(-TIME)*8)
113 FOR S2=0 TO 1 
114 K=1:L=0:C=R1:X1=128:Y1=105:X2=S1+S2+4:Y2=0:GOSUB 611
115 K=1:L=0:C=R1:X1=128:Y1=105:X2=S1+S2+4:Y2=210:GOSUB 611
116 NEXT S2
117 NEXT S1
121 FOR S1=0 TO 211 STEP 8
122 R1=INT(RND(-TIME)*8)
123 FOR S2=0 TO 1
124 K=1:L=0:C=R1:X1=128:Y1=105:X2=0:Y2=S1+S2:GOSUB 611
125 K=1:L=0:C=R1:X1=128:Y1=105:X2=254:Y2=S1+S2:GOSUB 611
126 NEXT S2
127 NEXT S1
163 BSAVE "HYPER2.SC5",0,256*212-1,S
200 GOTO 2036
600 '--------------------------------------------------------------------------------
610 'Bresenham based on: https://web.wakayama-u.ac.jp/~tokoi/lecture/cg/cgnote03.pdf
611 X=X1:Y=Y1
612 IX=1:DX=X2-X1:IF DX<0 THEN IX=-IX:DX=-DX
613 IY=1:DY=Y2-Y1:IF DY<0 THEN IY=-IY:DY=-DY
614 IF DX<DY THEN D=DY:E=-DY:A=2*DX:B=-2*DY:AX=0:AY=IY:BX=IX:BY=0 ELSE D=DX:E=-DX:A=2*DY:B=-2*DX:AX=IX:AY=0:BX=0:BY=IY
615 GOSUB 621
616 X=X+AX:Y=Y+AY:E=E+A:IF E>=0 THEN X=X+BX:Y=Y+BY:E=E+B 
617 D=D-1:IF D>=0 THEN GOTO 615 
618 RETURN
619 '--------------------------------------------------------------------------------
620 'SUBROUTINE FOR PUTTING THE PIXEL
621 GOSUB 1001:L=L+1:IF L=K THEN C=C+1:L=0:K=K+1
622 PSET(X,Y),C
811 RETURN
1000 'SUBROUTINE FOR CALCULATING INDEX OF PALETTE
1001 IF C=9 THEN C=2
1002 RETURN
2030 'MAIN ROUTINE FOR PALETTE AMINATION
2032 'DEFINE PALETTE COLORS
2036 P(0)=&H26:P(1)=&H26:P(2)=&H26:P(3)=&H26:P(4)=&H0:P(5)=&H0:P(6)=&H0:P(7)=&H0
2037 Q(0)=&H3:Q(1)=&H2:Q(2)=&H2:Q(3)=&H2:Q(4)=&H0:Q(5)=&H0:Q(6)=&H0:Q(7)=&H0
2040 'FRAME 1
2041 GOSUB 2123
2042 GOSUB 2216
2050 'FRAME 2
2051 GOSUB 2124
2052 GOSUB 2216
2060 'FRAME3
2061 GOSUB 2125
2062 GOSUB 2216
2070 'FRAME 4
2071 GOSUB 2126
2072 GOSUB 2216
2080 'FRAME 5
2081 GOSUB 2123
2085 GOSUB 2216
2090 'FRAME 6
2091 GOSUB 2124
2095 GOSUB 2216
2100'FRAME 7
2101 GOSUB 2125
2102 GOSUB 2216
2110'FRAME 8
2111 GOSUB 2126
2112 GOSUB 2216
2117 GOTO 2041
2122 'SHIFT PALETTES
2123 T=P(0):P(0)=P(4):P(4)=T:T=Q(0):Q(0)=Q(4):Q(4)=T:RETURN
2124 T=P(1):P(1)=P(5):P(5)=T:T=Q(1):Q(1)=Q(5):Q(5)=T:RETURN
2125 T=P(2):P(2)=P(6):P(6)=T:T=Q(2):Q(2)=Q(6):Q(6)=T:RETURN
2126 T=P(3):P(3)=P(7):P(7)=T:T=Q(3):Q(3)=Q(7):Q(7)=T:RETURN
2210' ASSIGN PALETTES 
2216 VDP(17)=2:OUT&H9A,P(0):OUT&H9A,Q(0):VDP(17)=3:OUT&H9A,P(1):OUT&H9A,Q(1):VDP(17)=4 :OUT&H9A,P(2):OUT&H9A,Q(2):VDP(17)=5:OUT&H9A,P(3):OUT&H9A,Q(3)
2217 VDP(17)=6:OUT&H9A,P(4):OUT&H9A,Q(4):VDP(17)=7:OUT&H9A,P(5):OUT&H9A,Q(5):VDP(17)=8:OUT&H9A,P(6):OUT&H9A,Q(6):VDP(17)=9:OUT&H9A,P(7):OUT&H9A,Q(7)
2220 RETURN
3010 I$=INPUT$(1)